:imagesdir: images
:toc: macro
:toc-title: Content
:sectnums:

//Hinweis: Footnotes werden im pdf als Endnotes angezeigt

[.text-center]
= Using ANIMALSCRIPT to Generate Animations

Covers ANIMAL Release 1.4

[.text-center]
Dipl.-Inform. Guido Rößling +
FB 12, Parallel Systems +
University of Siegen +
Hölderlinstr. 3 +
D-57080 Siegen

[.text-center]
EMail: roessling@acm.org

[.text-center]
April 6, 2000

toc::[]

include::introductionEnglish.adoc[]

include::requirementsEnglish.adoc[]

include::startingAnimalEnglish.adoc[]

== Generating A New Animation

In this example, you will use a few simple steps to generate a short but interesting animation about the behavior of the data structure _singly-linked list_.
This animation will illustrate how to use ANIMAL to easily visually build animations. +
The final result of this process will look roughly as follows:

image::Figure 2 Final result of the tutorial animation.jpg[align="center"]
[.text-center]
[[F2]]Figure 2: Final result of the tutorial animation

Don’t worry, reaching this result is really not difficult. But now, let’s get going!

=== Preparation for the new Animation
Generating animations using ANIMALSCRIPTbasically only requires an arbitrary _text editor_.
So, start your favourite text editor on a new file called – for example – demo.asu.
Note that the extension _.asu_ is reserved for _uncompressed_ ANIMALSCRIPT. +
ANIMALSCRIPTfiles generally start with the following header line:

 %Animal 1 . 4

Make this the first line of the file and end it with _Return_.
Note that the version number _1.4_ given in the file reflects the version of ANIMALSCRIPTused and thus may change in the future. +
Next, you might want to give some information about the animation itself.
Append these following lines, exchanging the title and author according to your wishes:

....
title "AnimalScript Tutorial Animation"
author "Guido Roessling <roessling@acm.org>"
....

Now, you can already start ANIMAL and load in your animation.
Start the ANIMAL player as described in <<Starting and Playing ANIMAL Animations>>.
After a while spent on initialization and loading the initial animation, (at least) ANIMAL’s main window is shown:

image::Figure 3 ANIMALs Main Window.jpg[]
[.text-center]
[[F3]]Figure 3: ANIMALs Main Window

This window contains menus for _file operations_ (`File`), opening and closing the windows (`Edit`) used for editing and viewing the animation, setting the _Options_ (menu `Options`), and `Help`.
Furthermore, it has a list of buttons which serve as a shortcut for – from left to right – _New Animation, Load Animation, Input_ ANIMALSCRIPT, _Save Animation, Save Animation As..._ +
Select _Load_ in one of the following ways:

* Click on the _second_ button in ANIMAL’s main window showing a _opened folder_,
* or click on the menu `File` and select its second entry, `Load`. +
You can also use shortcuts by pressing the shortcut key and the letter highlighted in the menu - in this case, `L`, so press both `ALT` and `L`, and the menu will be displayed.
If not, you probably have to replace `ALT` by `CTRL`.
If this does not work either, ask your system administrator for the local configuration details. +
After the menu is shown, pressing `L` – the letter shown after the entry `Load` – is the same as clicking on `New`.

You can see the title and author information in the _Animation Information_.
Select the menu `Help` and then `About this Animation` to show the information dialog.

=== Generating the Title

The first thing we are going to generate is the appropriate _title_.
As this is a simple text, we use the text command as follows:

 text "header" "List element demo" at (20, 30)

image::Figure 4 Selecting the displayed windows.jpg[]
[.text-center]
[[F4]]Figure 4: Selecting the displayed windows.
Here, both Animation and Draw Window are opened.

Reload your animation, and you should see the header, but possibly not quite with the right "look".
To change this, we assign the header a new font: `SansSerif`, size 24, *bold*.
To do so, simply change the line to the following:

 text "header" "List element demo" at (20, 30) font SansSerif size 24 bold

However, *do not break the line in your editor!*
All ANIMALSCRIPTcommands must be given in a single line, so you should probably turn off word wrapping, too.
Due to space limitations, the command might not fit in a single line in this display.
Your command lines can have an arbitrary length in the file, though. +
Reloading the animation will show you a somewhat more appropriate setting for a title.
Also notice that ANIMAL has set the text color to the default color _black_ without your saying so. +
Now, we want to underscore the importance of the title by placing a _filled rectangle_ below the title.
This requires the use of a second command, *rectangle*. +
There is a small problem in doing so, however.
Do you know just how high and wide the text is?
Experimenting with different setting would be rather time-consuming and tiresome, after all... +
Luckily, ANIMALSCRIPT addresses this problem by allowing _relative placement_.
Instead of giving the _absolute coordinates_ for the rectangle points, you can substitute one (or all) of them using _offsets_ from another object.
In this case, we know that the rectangle should cover the whole text, so it should start a bit to the upper left of the text and extend to the text’s lower right. +
Try the following line and see what happens when you reload the animation:

 rectangle "hRect" offset (-5, -5) from "header" NW offset (5, 5) from "header" SE

You will now see _two_ different animation steps, as each ANIMALSCRIPTcommand normally starts a new animation step.
To make sure both commands are executed in the _same_ step, put an opening curly brace { in a _single_ line before the *text* command, and a matching closing brace } in a separate line after the *rectangle* command.
Now reload and see what happens. +
Hmmm... the rectangle completely hides the text!
This is not really surprising, as ANIMAL draws the objects in the order they were specified.
Changing the order of the commands will not work, as the rectangle will then not have access to the text coordinates.
However, ANIMALSCRIPTalso offers the optional *depth* attribute for all graphic objects.
*depth* influences the order in which objects are drawn: objects with _less_ depth are _closer to the foreground_, and will be drawn _after_ object that lie "deeper".
Therefore, change your lines to the following:

....
{
text "header" "List element demo" at(20, 30) depth 1 font SansSerif size 24 bold
rectangle "hRect" offset (-5, -5) from "header" NW offset (5, 5) from "header" SE\
    depth 2 filled fillColor white
}
....

Reload and see that now the header behaves properly.
You might also try adding the following line to your animation:

 echo bounds: "header"

This will output the exact bounding box of the title when the animation is loaded in.

=== Embedding Code Lines

Now, we are going to enter the documentation for this animation.
This consists of the following text entries:

* `1. Generate first list element`
* `2. Set link of first list element to null`
* `3. Generate new list element`
* `4. Clear link of second list element`
* `5. Link first with second list element`
* `6. Generate new list element`
* `7. Link new with second list element`
* `8. Link first with new element`
* `9. Transform into ’nice’ list structure`

To enter this code, you could use the *text* command as shown previously.
However, ANIMALSCRIPT also offers the concept of a code group of related lines of code (in this case, pseudo code!) which allows operations such as _indentation_ and especially _highlighting_. +
To generate a new code group, type in the following command:

 codeGroup "listSource" at (10, 200) color black highlightColor red

Note that this command will _not_ enter a new graphic object at all, but will only prepare ANIMAL for handling code lines.
The coordinate (10, 200) is the _upper left corner_ for the code group.
Instead of the absolute coordinates, you could also have used relative placement, for example

 codeGroup "listSource" offset (0, 80) from "hRect" SW color black

To enter the code elements, you should place them _including_ the *codeGroup* command inside a _single_ animation step, so that they will all appear at the same time.
Therefore, put curly braces in the line before and after the *codeGroup* command. +
Entering a new code fragment is very easy - you only have to say

 addCodeLine "text" to "codeGroupID"

In our case, simply enter the following commands:

....
{
codeGroup "listSource" at (10, 200) color black highlightColor red
addCodeLine "1. Generate first list element" to "listSource"
addCodeLine "2. Set link of first list element to null" to "listSource"
addCodeLine "3. Generate new list element" to "listSource"
addCodeLine "4. Clear link of second list element" to "listSource"
addCodeLine "5. Link first with second list element" to "listSource"
addCodeLine "6. Generate new list element" to "listSource"
addCodeLine "7. Link new with second element" to "listSource"
addCodeLine "8. Link first with new element" to "listSource"
addCodeLine "9. Transform into 'nice' list structure" to "listSource"
}
....

Again, keep in mind that these commands must all appear on a single line each.

=== Code Highlighting

Now that the preparations are done, we can start with the contents of this animation.
The first command to be executed is the first code line.
To show this execution, you can highlight the code line as follows:

 highlightCode on "listSource" line 0

Note that as is usual in *C* and *Java*, ANIMALSCRIPTstarts counting at 0, so the first code line has number 0. +
The effect of this statement as shown on reloading the animation is setting the line in its _highlightColor_ *red*.
Check the *codeGroup* command you entered – the color *red* was explicitly given as the highlight color.
If you prefer other colors, feel free to change this entry.

=== Generating A List Element
Now that the command has been highlighted, you can generate the first list element.
This is done using the *listElement* command as follows:

 listelement "firstListElem" (100, 80) pointers 1

However, this leaves the element without a text entry.
Therefore, insert a *text "Elem 1"* between the coordinates and the *pointers* command.
Furthermore, the element should not be shown at once, as we first want to draw attention to the command itself.
Therefore, use the command as follows:

 listelement "elemA" (100, 80) text "Elem1" pointers 1 after 20 ticks

The *after* command determines the amount of time to wait until the action is performed - in this case, 20 ticks will take place before the element is shown.
*Ticks* are an ANIMAL-internal time unit that allows for smoother effects than precise timing on a millisecond base does
footnote:[If you are wondering about this, note that statements like after 200 ms require the computer to act at a precise point of time, or perform all operations within a presice amount of time.
If the computer is busy or the actions are time-consuming to perform, it may fall behind the schedule and try to catch up by skipping intermediate animation frames.
This will often lead to a jerky display.]. +
On reloading the animation, you will find that the operations have ended on separate steps, so change the code to include curly braces around the two commands.

=== Highlighting and Unhighlighting Code

Next, you should start working on the second command.
This requires the following operations to take place in _one_ step:

* unhighlight the first line of code (*line 0*),
* highlight the second line of code (*line 1*),
* clear the link of the new list element.

If you paid close attention, the second operation should not pose any problem by now.
Rather unsurprisingly, the command for _unhighlighting_ code is called *unhighlightCode*. +
Finally, the command for _clearing_ a link is called *clearLink* and requires you to give the name of the object to be treated this way. +
Thus, the next commands should look as follows:

....
{
unhighlightCode on "listSource" line 0
highlightCode on "listSource" line 1
clearLink "elemA"
}
....

Now, we will repeat these actions to generate the next list element, clear its link, and update the source highlighting accordingly.
The second element should be placed to the right of the first list element, say *(130, 0)* from the first list element’s top right corner (called *NW*).
The commands thus look as follows:

....
{
unhighlightCode on "listSource" line 1
highlightCode on "listSource" line 2
listelement "elemB" offset (130, 0) from "elemA" NE text "Elem2" pointers 1
}
{
unhighlightCode on "listSource" line 2
highlightCode on "listSource" line 3
clearLink "elemB"
}
....

=== Linking list elements

The next command requires you to link the two list elements.
This is easily accomplished using the *setLink* command, which expects first the name of the _base list element_, then the keyword *to* and the _target list element’s name_.
In this case, the operation should also take a certain amount of time, for example the *20 ticks* used before. +
The required commands are as follows:

....
{
unhighlightCode on "listSource" line 3
highlightCode on "listSource" line 4
setLink "elemA" to "elemB" within 20 ticks
}
....

Note that the *within* keyword defines the _duration_ of an operation, while *after* defines the _offset_.
If *both* are used, you have to specify the _offset_ first. +
I will leave the generation of the next few steps as a small exercise.
Using what you have done before, it should not be difficult to update the _code highlighting, generate_ a new list element called *elemC*, and setting the links between the list elements.
However, the complete code is also given a bit later in this document. +
Please place the new list element roughly in the middle of the two elements, but *not* precisely.
For example, place it at offset *(80, 50)* from the *NW* of the first list element.

=== Moving objects
The last step calls for transforming the list into a _nice_ structure.
To do so, you have to perform the following actions:

* Updating the code highlight for the last line of code (not described here),
* move the *new* element between the two list elements _without_ changing its pointer,
* move the first list element’s pointer without moving the rest of the object.

The last two operations call for a *move* operation which is quite difficult, but also shows how powerful ANIMALSCRIPT is. +
Note that the bounding box of any object is a rectangle encapsulating _all_ components of the element.
Thus, the bounding box of the _middle_ list element (the last one you inserted) also includes the _pointer_ to the last list element.
If you used the positioning given above, you will notice that the pointer extends the bounds to the top and the right. +
Now, we want to move both the new list element – _without_ changing its pointer! – *and* set the first element’s pointer _without_ changing the element’s position.
To do so, we first have to decide on how to move the elements.
The following screenshot should be helpful, showing the bounding box of the elements.

image::Image_Elem1_2_3.png[align="center", width=400]

If you take a close look, you will find that you can use the following edges of the bounding boxes for precise placement:

* the middle element’s *SE* corner, as this includes the base line of the object _and_ the pointer location;
* the *SW* corner of the last list element, as this gives the target baseline _and_ the target coordinates for the pointer.

First, we will generate a line object with these coordinates that should be marked as hidden to prevent it from disturbing the display:

 line "moveLine1" offset (0, 0) from "elemC" SE offset (0, 0) from "elemB" SW hidden

Now, we can use this line for moving both objects.
However, we have to use two _special_ subtypes of the *move* command for this purpose:

* for setting only the pointer _without_ changing the element, we need a move of *type "setTip #1"*
* and for only setting the pointer _without_ moving the element, we need *type "translateWithFixedTip"*

The following code does the trick:

....
move "elemC" type "translateWithFixedTip" via "moveLine1" after 20 ticks within\
    20 ticks
move "elemA" type "setTip #1" via "moveLine1" after 20 ms within 20 ticks
....

And now... the animation is finished!
Simply reload and enjoy. +
The complete animation code is as follows:

....
%Animal 1 . 4
title "AnimalScript Tutorial Animation"
author "Guido Roessling <roessling@acm.org>"
{
text "header" "Listelement demo" at(20, 30) depth 1 font SansSerif size 24 bold
rectangle "hRect" offset (-5, -5) from "header" NW offset (5, 5) from "header" SE\
    depth 2 filled fillColor white
}
{
codeGroup "listSource" at (10, 200) color black highlightColor red
addCodeLine "1. Generate first list element" to "listSource"
addCodeLine "2. Set link of first list element to null" to "listSource"
addCodeLine "3. Generate new list element" to "listSource"
addCodeLine "4. Clear link of second list element" to "listSource"
addCodeLine "5. Link first with second list element" to "listSource"
addCodeLine "6. Generate new list element" to "listSource"
addCodeLine "7. Link new with second element" to "listSource"
addCodeLine "8. Link first with new element" to "listSource"
addCodeLine "9. Transform into 'nice' list structure" to "listSource"
}
{
highlightCode on "listSource" line 0
listelement "elemA" (100, 80) text "Elem1" pointers 1 after 20 ticks
}
unhighlightCode on "listSource" line 0
highlightCode on "listSource" line 1
clearLink "elemA"
}
{
unhighlightCode on "listSource" line 1
highlightCode on "listSource" line 2
listelement "elemB" offset (130, 0) from "elemA" NE text "Elem2" pointers 1
}
{
unhighlightCode on "listSource" line 2
highlightCode on "listSource" line 3
clearLink "elemB"
}
{
unhighlightCode on "listSource" line 3
highlightCode on "listSource" line 4
setLink "elemA" to "elemB" within 20 ticks
}
{
unhighlightCode on "listSource" line 4
highlightCode on "listSource" line 5
listelement "elemC" offset (80, 50) from "ElemA" NW text "elem3" pointers 1
}
{
unhighlightCode on "listSource" line 5
highlightCode on "listSource" line 6
setLink "elemA" link 1 to "elemC"
}
{
unhighlightCode on "listSource" line 6
highlightCode on "listSource" line 7
setLink "elemC" link 1 to "elemB"
}
{
unhighlightCode on "listSource" line 7
highlightCode on "listSource" line 8
line "moveLine1" offset (0, 0) from "elemC" SE offset (0, 0) from "elemB" SW hidden
move "elemC" type "translateWithFixedTip" via "moveLine1" after 20 ticks\
    within 20 ticks
move "elemA" type "setTip #1" via "moveLine1" after 20 ms within 20 ticks
}
....
